prontera,150,193,3	script	Code Redeem::REDEEMCODE	833,{
	function Add_Zero; function Add_Zero_Day; function Leap_Year; function Select_Time;
	function Select_Year; function Select_Month; function Select_Day; function Bound_Type;

L_Menu:
	mes .name$;
	mes (gettime(3)>= 6&&gettime(3)<= 12?"Good Morning":(gettime(3)>=13&&gettime(3)<=18?"Good afternoon":"Good Night"))+", ^FFA500"+strcharinfo(0)+"^000000!";
	mes "How may i assist you?.";
	next;
	switch(select("Redeem Code:Information:"+( getgmlevel()>=99 ? "^FF5500Management^000000":"")+":Cancel")){
	case 1:
		callsub OnRedeemCode;
		clear;
		goto L_Menu;
	case 2:
		mes .name$;
		mes "Promotional Codes is a ^0055FFHASH^000000";
		mes "data, that will allow you to";
		mes "redeem set of items.";
		next;
		mes .name$;
		mes "All Codes are given directly to a person";
		mes "or posted through the announcement page.";
		mes "So, be sure to always check web announcement";
		next;
		goto L_Menu;
	case 3:
		callsub OnAdminPanel;
	case 4:
		end;
	}
end;

OnAdminPanel:
	next;
	mes .name$;
	mes "Hello Master!~";
	mes "What would you like to do?.";
	next;
	switch(select("~ CREATE CODE:~ DELETE CODE:~ CHECK CODES:~ Back")){
		case 1:
			callsub OnCreateCode; break;
		case 2:
			.@count = query_sql("SELECT * FROM `promo_code`",.@id,.@cname$,.@code$,.@item$,.@amount,.@sdate$,.@edate$);
			mes .name$;
			mes "Please select a code you want to delete.";
			next; .@mcode$ = "";
			for(.@i=0;.@i<.@count;.@i++)
				.@mcode$ = .@mcode$ + (.@i+1)+". "+.@cname$[.@i]+":";
			.@mcode$ = .@mcode$ + "Back";
			.@scode = select(.@mcode$)-1;
			if(.@scode >= .@count) callsub OnAdminPanel;
			mes .name$;
			mes "Are you sure you want to delete this?";
			mes "===================================";
			mes "^0055FFPromo Name :^000000 "+.@cname$[.@scode];
			mes "^0055FFPromo Code :^000000 "+.@code$[.@scode];
			mes "^0055FFItem List :^000000 ";
			explode(.@list$,.@item$[.@scode],","); .@a = 0;
			for(.@i=0;.@i<getarraysize(.@list$);.@i+=3)
				mes (++.@a)+". ^32CD32"+atoi(.@list$[.@i+1])+"x "+getitemname(atoi(.@list$[.@i]))+Bound_Type(atoi(.@list$[.@i+2]))+"^000000";
			mes "^0055FFStart Date :^000000 "+.@sdate$[.@scode];
			mes "^0055FFEnd Date :^000000 "+.@edate$[.@scode];
			mes "^0055FFRedeemable Count :^000000 "+(.@amount[.@scode] >= 999999 ? "Indefinite":.@amount[.@scode])+"";
			next;
			if(select("~ Delete:~ Cancel:")==2) callsub OnAdminPanel;
			mes .name$;
			mes "Deleting Promo Code....";
			mes "^FFFFFF_^000000";
			query_sql("DELETE FROM `promo_code` WHERE id='"+.@id[.@scode]+"'");
			sleep2 3000;
			mes "Delete Successful!~";
			callsub OnAdminPanel;
		case 3:
			.@count = query_sql("SELECT * FROM `promo_code`",.@id,.@cname$,.@code$,.@item$,.@amount,.@sdate$,.@edate$);
			mes .name$;
			mes "Please select a code you want to review.";
			next; .@mcode$ = "";
			for(.@i=0;.@i<.@count;.@i++)
				.@mcode$ = .@mcode$ + (.@i+1)+". "+.@cname$[.@i]+":";
			.@mcode$ = .@mcode$ + "Back";
			.@scode = select(.@mcode$)-1;
			if(.@scode >= .@count) callsub OnAdminPanel;
			mes .name$;
			mes "^0055FFPromo Name :^000000 "+.@cname$[.@scode];
			mes "^0055FFPromo Code :^000000 "+.@code$[.@scode];
			mes "^0055FFItem List :^000000 ";
			explode(.@list$,.@item$[.@scode],","); .@a = 0;
			for(.@i=0;.@i<getarraysize(.@list$);.@i+=3)
				mes (++.@a)+". ^32CD32"+atoi(.@list$[.@i+1])+"x "+getitemname(atoi(.@list$[.@i]))+Bound_Type(atoi(.@list$[.@i+2]))+"^000000";
			mes "^0055FFStart Date :^000000 "+.@sdate$[.@scode];
			mes "^0055FFEnd Date :^000000 "+.@edate$[.@scode];
			mes "^0055FFRedeemable Count :^000000 "+(.@amount[.@scode] >= 999999 ? "Indefinite":.@amount[.@scode])+"";
			callsub OnAdminPanel;
		case 4:
			clear;
			goto L_Menu;
	}
end;

OnRedeemCode:
while(1){
	if(.@a==1){
		if(select("Insert Code:Back")==2) return;
		next;
	}
	.@a = 1;
	mes .name$;
	mes "Please Insert the code...";
	mes "^FFFFFF_^000000";
	mes "^FF5500Note: This is Case Sensitive.";
	next;
	input .@incode$;
	.@count = query_sql("SELECT * FROM `promo_code` WHERE `code` = '"+escape_sql(.@incode$)+"'",.@id,.@cname$,.@code$,.@item$,.@amount,.@sdate$,.@edate$);
	mes .name$;
	if(.@count <= 0){
		mes "There's no such code.";
		continue;
	}
	mes "Here's the content of the code.";
	mes "^0055FFPromo Name :^000000 "+.@cname$[.@scode];
	mes "^0055FFPromo Code :^000000 "+.@code$[.@scode];
	mes "^0055FFItem List :^000000 ";
	explode(.@list$,.@item$[.@scode],","); .@a = 0;
	for(.@i=0;.@i<getarraysize(.@list$);.@i+=3)
		mes (++.@a)+". ^32CD32"+atoi(.@list$[.@i+1])+"x "+getitemname(atoi(.@list$[.@i]))+Bound_Type(atoi(.@list$[.@i+2]))+"^000000";
	next;
	if(select("~ Redeem: ~Back")==2) return;
	.@count = query_sql("SELECT * FROM `promo_code` WHERE `code` = '"+escape_sql(.@incode$)+"'",.@id,.@cname$,.@code$,.@item$,.@amount,.@sdate$,.@edate$);
	mes .name$;
	if(.@amount <= 0){
		mes "This code is not redeemable anymore.";
		continue;
	}
	if(getd("#REDEEM_"+.@id)){
		mes "You already received this code.";
		continue;
	}
	if(gettimestr("%Y-%m-%d %H:%M:%S",21,gettimetick(2))>.@edate$){
		mes "This Code already expired.";
		continue;
	}
	if(gettimestr("%Y-%m-%d %H:%M:%S",21,gettimetick(2))<.@sdate$){
		mes "This Code is not yet redeemable.";
		continue;
	}
	explode(.@list$,.@item$[.@scode],",");
	for(.@i=0;.@i<getarraysize(.@list$);.@i+=3){
		.@getiid[.@m++] = atoi(.@list$[.@i]);
		.@getamt[.@n++] = atoi(.@list$[.@i+1]);
	}
	if(!checkweight2(.@getiid,.@getamt)){
		mes "Please make more space on your inventory.";
		end;
	}
	query_sql("UPDATE `promo_code` SET `count` = '"+(.@amount-1)+"' WHERE `id` = '"+.@id+"';");
	setd "#REDEEM_"+.@id,1;
	logmes strcharinfo(0)+" redeemed promo id "+.@id; // Logs
	for(.@i=0;.@i<getarraysize(.@list$);.@i+=3){
		switch(atoi(.@list$[.@i+2])){
			case 3:
				getitembound atoi(.@list$[.@i]),atoi(.@list$[.@i+1]),Bound_Char;
				break;
			case 2:
				getitembound atoi(.@list$[.@i]),atoi(.@list$[.@i+1]),Bound_Account;
				break;
			case 1:
			default:
				getitem atoi(.@list$[.@i]),atoi(.@list$[.@i+1]);
				break;
		}
	}
	mes "You successfully redeemed your code.";
	mes "Enjoy your day~";
	next;
	break;
}
return;

OnCreateCode:
freeloop(1);
.@loop = 1;
while(.@loop){
	mes .name$;
	if(!getarraysize(.@iid))
		mes "^D3D3D3No items Added.";
	else
		for(.@i=0;.@i<getarraysize(.@iid);.@i++)
			mes (.@i+1)+". ^32CD32"+.@amt[.@i]+"x "+getitemname(.@iid[.@i])+Bound_Type(.@bound[.@i])+"^000000";
	next;
	switch(select("~ Add Item:~ Remove Item:~ Create Code:~ Cancel")){
		case 1:
				message strcharinfo(0),"Insert Item ID";
				input .@inid;
				if(getiteminfo(.@inid,2)==-1){
					message strcharinfo(0),"Invalid ID";
					break;
				}
				message strcharinfo(0),"Insert Amount";
				input .@inamt,1,30000;
				message strcharinfo(0),"Bound Type";
				.@inbound = select("~ None:~ Account:~ Character");
				.@iid[getarraysize(.@iid)] = .@inid;
				.@amt[getarraysize(.@amt)] = .@inamt;
				.@bound[getarraysize(.@bound)] = .@inbound;
				break;
		case 2:
			mes .name$;
			mes "Select Item you want to remove.";
			next;
			.@menu$="";
			for(.@i=0;.@i<getarraysize(.@iid);.@i++)
				.@menu$ = .@menu$ + .@amt[.@i]+"x "+getitemname(.@iid[.@i])+Bound_Type(.@bound[.@i])+":";
			.@menu$ = .@menu$ + "Back";
			.@rmv = select(.@menu$)-1;
			if(.@rmv>=getarraysize(.@iid)) break;
			mes .name$;
			mes "Sure you want to delete";
			mes .@amt[.@rmv]+"x "+getitemname(.@iid[.@rmv])+Bound_Type(.@bound[.@rmv])+"?";
			next;
			if(select("Remove:Cancel")==2) break;
			mes .name$;
			mes "Remove Successful~";
			deletearray .@iid[.@rmv],1;
			deletearray .@amt[.@rmv],1;
			deletearray .@bound[.@rmv],1;
			break;
		case 3:
			.@loop = 0; break;
		case 4:
			end;
	}
	next;
}
	mes .name$;
	mes "Please select the year it start.";
	next;
	.@year1 = Select_Year;
	mes .name$;
	mes "Please select the month it start.";
	next;
	.@month1 = Select_Month;
	mes .name$;
	mes "Please select the day it start.";
	next;
	.@day1 = Select_Day(.@month1,.@year1);
	mes .name$;
	mes "Please select the time it start.";
	next;
	.@time1$ = Select_Time;
	mes .name$;
	mes "The Start Date has beeen decided.";
	mes "Now, Please set the expiration date.";
	next;
	mes .name$;
	mes "Please select the year it expires.";
	next;
	.@year2 = Select_Year;
	mes .name$;
	mes "Please select the month it expires.";
	next;
	.@month2 = Select_Month;
	mes .name$;
	mes "Please select the day it expires.";
	next;
	.@day2 = Select_Day(.@month2,.@year2);
	mes .name$;
	mes "Please select the time it expires.";
	next;
	.@time2$ = Select_Time;
	mes .name$;
	mes "The Start Date and Expiration Date has been decided.";
	mes "How many times do you want this code redeemed?";
	mes "^FF5500Note: Insert 9999 for indefinite amount.";
	next;
	input .@count,1,32767;
	mes .name$;
	mes "Please enter the name of this Promotional Code.";
	next;
	input .@NAME$;
	.@CODE$ = substr(strtoupper(md5(""+gettimetick(2))),0,11);
	.@START$ = .@year1+"-"+Add_Zero_Day(.@month1)+"-"+Add_Zero_Day(.@day1)+" "+Add_Zero(atoi(.@time1$))+":00";
	.@END$ = .@year2+"-"+Add_Zero_Day(.@month2)+"-"+Add_Zero_Day(.@day2)+" "+Add_Zero(atoi(.@time2$))+":00";
	mes .name$;
	mes "All Setup!~";
	mes "Please confirm the details.";
	next;
	mes .name$;
	mes "^0055FFPromo Name :^000000 "+.@NAME$;
	mes "^0055FFPromo Code :^000000 "+.@CODE$;
	mes "^0055FFItem List :^000000 ";
	for(.@i=0;.@i<getarraysize(.@iid);.@i++)
		mes (.@i+1)+". ^32CD32"+.@amt[.@i]+"x "+getitemname(.@iid[.@i])+Bound_Type(.@bound[.@i])+"^000000";
	mes "^0055FFStart Date :^000000 "+.@START$;
	mes "^0055FFEnd Date :^000000 "+.@END$;
	mes "^0055FFRedeemable Count :^000000 "+(.@count == 9999 ? "Indefinite":.@count)+"";
	next;
	if(select("~ Create!:~ Cancel")==2) end;
	mes .name$;
	mes "Creating Promotional Code...";
	mes "^FFFFFF_^000000";
	while(1){
		.@ITEM$ = .@ITEM$ + .@iid[.@j] + "," + .@amt[.@j] + "," + .@bound[.@j];
		.@j++;
		if(.@j>=getarraysize(.@iid))
			break;
		else
			.@ITEM$ = .@ITEM$ + ",";
	}
	query_sql("INSERT INTO `promo_code` VALUES (NULL,'"+escape_sql(.@NAME$)+"', '"+.@CODE$+"', '"+.@ITEM$+"', '"+(.@count == 9999 ? "999999999":""+.@count)+"', '"+.@START$+"', '"+.@END$+"');");
	sleep2 3000;
	mes "Successfully Created!";
freeloop(0);
return;

OnInit:
	//waitingroom "Reedeem Here!",0;

	.name$ = "^00B2EE[ Promotional Code ]^000000";
	
	setarray .Month$[1],"January","February","March","April",
						"May","June","July","August","September",
						"October","November","December";
						
	query_sql("CREATE TABLE IF NOT EXISTS promo_code ("+
				"id int(255) NOT NULL AUTO_INCREMENT,"+
				"code_name varchar(255) NOT NULL,"+
				"code varchar(255) NOT NULL,"+
				"item varchar(255) NOT NULL,"+
				"count int(255) NOT NULL,"+
				"start_date datetime NOT NULL,"+
				"end_date datetime NOT NULL,"+
				"PRIMARY KEY (id)"+
				") ENGINE=InnoDB DEFAULT CHARSET=latin1;");
	PUBTITLE:
		showscript "Promotional Code";
		sleep 3000;	
	goto PUBTITLE;
end;

function Leap_Year {
  if (getarg(0) % 4 == 0) {
        if (getarg(0) % 100 == 0) {
            if (getarg(0) % 400 == 0)
                return 1;
            else
                return 0;
        } else
            return 1;
    } else
        return 0;
}

function Add_Zero {
	return ((getarg(0)<10)?"0":"")+getarg(0)+(getarg(1,0)?".":":")+"00";
}

function Add_Zero_Day {
	return ((getarg(0)<10)?"0":"")+getarg(0);
}

function Select_Year {
	for( .@i=0; .@i<=20; .@i++)
		.@year$ = .@year$+" ~ "+(gettime(DT_YEAR)+.@i)+":";
	return (gettime(DT_YEAR)+select(.@year$))-1;
}

function Select_Month {
	for( .@i=1; .@i<=12; .@i++)
		.@month$ = .@month$+" ~ "+.Month$[.@i]+":";
	return select(.@month$);
}

function Select_Day {
	.@leap = Leap_Year(getarg(1));
	for( .@i=1; .@i<=( getarg(0)==2 ? 28+.@leap:30+(getarg(0)%2) ); .@i++)	//day
		.@day$ = .@day$+" ~ "+.@i+":";
	return select(.@day$);
}

function Select_Time {
	for( .@i=0; .@i<=23; .@i++)
		set .@time$, .@time$+" ~ "+Add_Zero(.@i,1)+":";
	return select(.@time$)-1;
}

function Bound_Type {
	switch(getarg(0)){
	case 3:
		return "[Character]";
	case 2:
		return "[Account]";
	case 1:
	default: 
		return "[None]";
	}
}
}

//clashcity,122,140,5	duplicate(REDEEMCODE)	Code Redeem#main	10967
amatsu,125,86,3	duplicate(REDEEMCODE)	Code Redeem#amatsu	10967
jawaii,216,286,3	duplicate(REDEEMCODE)	Code Redeem#jawaii	10967
thor_camp,114,324,3	duplicate(REDEEMCODE)	Code Redeem#thorcamp	10967
malaya,296,329,4	duplicate(REDEEMCODE)	Code Redeem#malaya	10967
ghouse1,165,124,3	duplicate(REDEEMCODE)	Code Redeem#ghouse1	10967
ghouse2,165,124,3	duplicate(REDEEMCODE)	Code Redeem#ghouse2	10967
ghouse3,165,124,3	duplicate(REDEEMCODE)	Code Redeem#ghouse3	10967